import org.springframework.boot.gradle.tasks.bundling.BootJar

plugins {
	id 'java'
	id 'org.springframework.boot' version '3.3.8-SNAPSHOT'
	id 'io.spring.dependency-management' version '1.1.7'
}

group = 'com.tankmilu'
version = '0.0.1-SNAPSHOT'

java {
	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
	maven { url 'https://repo.spring.io/snapshot' }
}

dependencies {
	implementation 'io.asyncer:r2dbc-mysql:1.3.1'
	implementation 'com.eygraber:ffmpeg-cli-wrapper:0.9.0'
	implementation 'io.jsonwebtoken:jjwt-api:0.12.6'
	runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.12.6', 'io.jsonwebtoken:jjwt-jackson:0.12.6'
	implementation 'com.mysql:mysql-connector-j:8.3.0'
	implementation("com.googlecode.juniversalchardet:juniversalchardet:1.0.3")
	implementation 'org.springframework.boot:spring-boot-starter-webflux:3.3.8'
	implementation 'org.springframework.boot:spring-boot-starter-security:3.3.8'
	implementation 'org.springframework.boot:spring-boot-starter-batch:3.3.8'
	implementation 'org.springframework.boot:spring-boot-starter-data-r2dbc:3.3.8'
	implementation 'org.springframework.boot:spring-boot-starter-data-elasticsearch:3.3.8'
	compileOnly 'org.projectlombok:lombok:1.18.36'
	annotationProcessor 'org.projectlombok:lombok:1.18.36'
	developmentOnly 'org.springframework.boot:spring-boot-devtools:3.3.8-SNAPSHOT'
	implementation 'com.h2database:h2:2.2.224'
	implementation 'io.r2dbc:r2dbc-h2:1.0.0.RELEASE'
	testImplementation 'org.springframework.boot:spring-boot-starter-test:3.3.8'
	testImplementation 'io.projectreactor:reactor-test:3.6.13'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher:1.10.5'

}

tasks.named('test') {
	useJUnitPlatform()
}

bootJar {
    enabled = false
}

tasks.register('webfluxJar', BootJar) {
	group = 'build'
	description = 'Builds a JAR file for the Webflux application.'
	mainClass = 'com.tankmilu.webflux.WebfluxApplication'
	archiveClassifier = 'webflux'
	targetJavaVersion = java.targetCompatibility
	classpath = sourceSets.main.runtimeClasspath
}

tasks.register('batchJar', BootJar) {
	group = 'build'
	description = 'Builds a JAR file for the Batch application.'
	mainClass = 'com.tankmilu.batch.BatchApplication'
	archiveClassifier = 'batch'
	targetJavaVersion = java.targetCompatibility
	classpath = sourceSets.main.runtimeClasspath
}

